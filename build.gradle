apply plugin: 'idea'
apply plugin: 'scala'

compileJava.options.fork = ['-Xmx512m -XX:MaxPermSize=768m -XX:PermSize=512m']

ideaProject {
    beforeConfigured { project ->
        project.modulePaths.clear()
    }
    withXml { provider ->
        provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'scala'

    repositories {
       mavenRepo(urls: ['http://www.scala-tools.org/repo-releases/', 'http://akka.io/repository', 'http://maven.twttr.com/', 'https://repository.jboss.org/nexus/content/groups/public-jboss/', 'https://oss.sonatype.org/content/repositories/releases', 'http://repository.codehaus.org/org/codehaus', 'http://guiceyfruit.googlecode.com/svn/repo/releases/','http://maven.ala.org.au/repository','http://download.java.net/maven/2'])
       mavenCentral()
    }

    configurations {
        scalaTest
    }

    dependencies {
        scalaTools 'org.scala-lang:scala-compiler:2.9.1'
        scalaTools 'org.scala-lang:scala-library:2.9.1'

        compile group: 'org.clapper', name:'grizzled-slf4j_2.9.1', version:'0.6.6'

        testCompile group: 'org.scalatest', name: 'scalatest_2.9.1', version: '1.6.1'
        testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
        testCompile group: 'org.mockito', name:'mockito-core', version: '1.8.1'
        testCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
    }

    version = '0.1'

    jar {
        manifest.attributes provider: 'logserver'
    }

    task test (overwrite: true, dependsOn: testClasses) << {
        ant.taskdef(
            name: 'scalatest',
            classname: 'org.scalatest.tools.ScalaTestAntTask',
            classpath: configurations.testCompile.asPath + ':' + compileScala.destinationDir
        )
        ant.scalatest(
            runpath: sourceSets.test.classesDir,
            haltonfailure: 'true',
            fork: 'false'
        )
    }
}

